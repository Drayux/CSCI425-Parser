MODULE    -> MODPARTS $

MODPARTS  -> GCTDECLLIST sc | GCTDECLLIST sc MODPARTS
           | GFTDECLLIST sc | GFTDECLLIST sc MODPARTS
           | FUNSIG sc | FUNSIG sc MODPARTS
		   | FUNCTION  | FUNCTION  MODPARTS
		   | EMIT   sc | EMIT   sc MODPARTS

GCTDECLLIST -> GLOBTYPE DECLIDS
GFTDECLLIST ->  FUNTYPE DECLIDS

GLOBTYPE  -> const bool
           | const int
		   | const float
		   | string

FUNSIG    -> FUNTYPE id lparen PARAMLIST rparen
FUNTYPE   -> bool | int | float 
PARAMLIST -> FUNTYPE id comma PARAMLIST 
           | FUNTYPE id
		   | NOPARAMS
NOPARAMS  -> lambda

FUNCTION  -> FUNSIG returns id assign EXPR BRACESTMTS
BRACESTMTS -> lbrace STMTS rbrace
STMTS     -> STMTS STATEMENT
           | lambda
STATEMENT -> BRACESTMTS
           | DECLLIST sc
           | ASSIGN sc
           | IF
           | IFELSE
           | WHILE
           | EMIT sc

DECLTYPE  -> const bool
           | bool
           | const int
           | int
           | string
           | const float
           | float
DECLID    -> id 
           | ASSIGN
DECLIDS   -> DECLID
           | DECLIDS comma DECLID
DECLLIST  -> DECLTYPE DECLIDS 

ASSIGN    -> id assign EXPR 
           | id assign ASSIGN

IF        -> if lparen BEXPR rparen STATEMENT
IFELSE    -> if lparen BEXPR rparen BRACESTMTS else STATEMENT
WHILE     -> while lparen BEXPR rparen STATEMENT

EXPR      -> BEXPR | AEXPR | FUNCALL

FUNCALL   -> id lparen ARGLIST rparen
ARGLIST   -> ARGLIST comma EXPR
           | EXPR
		   | lambda

BOOLS     -> lt | leq | eq | geq | gt
BEXPR     -> AEXPR BOOLS AEXPR

PLUS      -> plus | minus
TIMES     -> mult | div | mod
AEXPR     -> SUM
SUM       -> SUM PLUS PRODUCT
           | PRODUCT
PRODUCT   -> PRODUCT TIMES VALUE
           | VALUE
VALUE     -> lparen EXPR rparen 
           | intval
           | floatval
           | stringval
           | id 
           | UNARY
           | CAST
UNARY     -> PLUS VALUE
           | not VALUE
           | compl VALUE
CAST      -> bool lparen EXPR rparen
           | int lparen EXPR rparen
           | float lparen EXPR rparen

EMIT      -> emit id comma AEXPR comma AEXPR
           | emit lparen AEXPR rparen
           | emit symtable

